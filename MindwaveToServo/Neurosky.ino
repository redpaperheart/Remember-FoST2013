
////////////////////////////////
// Read data from Serial UART //
////////////////////////////////
byte readFirstByte() {
  // trying to make it non blocking
  int ByteRead = 0x00;
  if(Serial.available()){
    ByteRead = Serial.read();
  }
  return ByteRead;
}

byte ReadOneByte() {
  int ByteRead;
  int n = 0;
  while(!Serial.available() && n < 10000) n++;
  ByteRead = Serial.read();
  return ByteRead;
}

void parseMindwave(){
  bigPacket = false;

  // Look for sync bytes
  if(readFirstByte() == 170) {
    if(ReadOneByte() == 170) {

      payloadLength = ReadOneByte();
      if(payloadLength > 169)                      //Payload length can not be greater than 169
          return;

      generatedChecksum = 0;        
      for(int i = 0; i < payloadLength; i++) {  
        payloadData[i] = ReadOneByte();            //Read payload into memory
        generatedChecksum += payloadData[i];
      }   

      checksum = ReadOneByte();                      //Read checksum byte from stream      
      generatedChecksum = 255 - generatedChecksum;   //Take one's compliment of generated checksum

        if(checksum == generatedChecksum) {    

        poorQuality = 200;
        attention = 0;
        meditation = 0;

        for(int i = 0; i < payloadLength; i++) {    // Parse the payload
          switch (payloadData[i]) {
          case 2:
            i++;            
            poorQuality = payloadData[i];
            bigPacket = true;            
            break;
          case 4:
            i++;
            attention = payloadData[i];                        
            break;
          case 5:
            i++;
            meditation = payloadData[i];
            break;
          case 0x80:
            i = i + 3;
            break;
          case 0x83:
            i = i + 25;      
            break;
          default:
            break;
          } 
        }

        if(bigPacket){
          lastReceivedPacket = millis();
        }

      }
      else {
        // Checksum Error
      }  // end if else for checksum
    } // end if read 0xAA byte
  } // end if read 0xAA byte
}















